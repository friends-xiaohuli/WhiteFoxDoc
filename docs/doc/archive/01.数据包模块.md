---
title: "数据包 教程 归档"
---
# 数据包各部分教程

哈喽大家好！

这里是霜澪，此节将贴出我做数据包可能会经常用到的模块部分。

![00](/img/02.gif)

::: tip
请将所有需要的计分板自主初始化哦！
:::

## 生成随机数

### 1.20.2（不包含）以前旧版可用随机数

通过除法限制最大值，求余随机

从0随机到除数max最大值

```text
#召唤一个带有标签r无重力无AI隐身且标记的盔甲架
summon armor_stand ~1 ~ ~ {"Tags":["r"],"Invulnerable":true,"NoGravity":true,Invisible:true,Marker:true}
#存储随机数r到计分板 -> 获取生成盔甲架的uuid的第一串字符 （UUID用4个32位数字表示，共计128位）
execute store result score r random run data get entity @e[tag=r,limit=1] UUID[0]
#求余（取模）赋值： 将 max 分数和 r 分数相除，取整数除法所得的正整数余数赋给目标分数。
#如 UUID [I;-132296786,2112623056,-1486552928,-920753162] 
# 被除数-132296786/ 除数60 = 商-2204947 余34
#本轮随机数为34
scoreboard players operation r random %= max random
#清理目标
kill @e[tag=r,type=armor_stand]

```

### 1.20.2+通过更新的random指令存储

可以使用宏函数进行**动态**最大值的传参

`function <命名空间ID>:random with storage <命名空间ID>:<自定storageID> args.N`

> 其中`<命名空间ID>:random`为下面function代码的位置
>
> `<命名空间ID>:<自定storageID>`此处为了防止冲突和修改，仍建议使用命名空间作为前缀命名
>
> with 携带含参存储storage 传入目标要求args.N下有 键值对的键为max才可被下面的读取到
>
> 如 `{'args':{'N':{'max': "60"}}}` 读取到的max即为60

```text
$execute store result score r random run random value 0..$(max) random
```

如不需要动态传参，此处最小值和最大值均可自定。

## 获取玩家人数

**存储重置计分板目标数量获取**。

存储子命令获取重置玩家的数量从而赋值max为玩家人数

```text
scoreboard players set @a player 114514
execute store result score max player run scoreboard players reset @a player
```

## 初始化/跳过初始化

即无论怎么重载数据包都只生效一次。

适用于需要存档多次进度而不被初始化的部分。

```text
scoreboard objectives add first dummy
execute unless score first first matches 0.. run function ini
```

在初始化运行后设置积分项`first`任何大于1的数值使得每次加载的时候跳过。

## 显示时间（换算实时时间）

我们依旧利用按照`20tick` 等于 现实里`1秒`的

`时分秒` 各自换算60秒的差值 取余数后再计算

缓存 `ss mm hh` 实际上的显示是 `s m h` 原始时间tick为 `time`

```text
# scoreboard objectives setdisplay sidebar time
# scoreboard players set time time 135790

scoreboard players operation ss time = time time
scoreboard players set t time 20
scoreboard players operation ss time /= t time

scoreboard players operation s time = ss time
scoreboard players set t time 60
scoreboard players operation s time %= t time

scoreboard players operation mm time = ss time
scoreboard players operation mm time /= t time

scoreboard players operation m time = mm time
scoreboard players operation m time %= t time

scoreboard players operation hh time = mm time
scoreboard players operation hh time /= t time

scoreboard players operation h time = hh time
scoreboard players operation h time %= t time
```

外显示例

```text
tellraw @a [{"text": "单局游戏总用时：","color": "green","bold": true},{"score":{"name":"h","objective":"time"},"bold": true,"color": "dark_red"},{"text": ":","bold": true,"color": "dark_red"},{"score":{"name":"m","objective":"time"},"bold": true,"color": "dark_red"},{"text": ":","bold": true,"color": "dark_red"},{"score":{"name":"s","objective":"time"},"bold": true,"color": "dark_red"}]
```
